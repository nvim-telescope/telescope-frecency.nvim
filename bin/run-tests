#!/usr/bin/perl

=head1 DESCRIPTION

    Utility to do tests

=head1 SYNOPSIS

    bin/run-tests
    # Run all tests

    bin/run-tests lua/frecency/tests/sorter_spec.lua
    # Run specified test

    bin/run-tests -e /path/to/nvim
    # Use another Neovim binary

    bin/run-tests -v
    # Show verbose log

=cut

use 5.24.0;
use warnings;
use feature 'signatures';
no warnings 'experimental::signatures';
use Getopt::Long qw(:config posix_default no_ignore_case bundling auto_help);
use JSON::PP;
use File::Spec;
use File::Path qw(make_path);
use File::Temp qw(tempdir);
use Time::Piece;
BEGIN {
    # HACK: Perl on GHA Windows has no Pod::Usage ??
    if (defined $ENV{RUNNER_OS} && $ENV{RUNNER_OS} eq 'Windows') {
        *pod2usage = sub {};
    } else {
        # do the same thing as `use Pod::Usage`
        require Pod::Usage;
        Pod::Usage->import(qw(pod2usage));
    }
}

my %opt = (
    executable => 'nvim',
);
GetOptions(
    \%opt, qw(
    update_libs|u
    executable|e=s
    verbose|v
    help|h
)) or pod2usage(1);
$opt{help} and pod2usage(0);

my $test_file = shift;

my $test_dir = 'lua/frecency/tests/';
my $minimal_lua = "${test_dir}minimal.lua";
my $command = defined $test_file
    ? qq(PlenaryBustedFile $test_file)
    : qq(PlenaryBustedDirectory $test_dir {minimal_init = '$minimal_lua'});

sub logger($msg) {
    if ($opt{verbose}) {
        say "[run-tests]: $msg";
    }
}

sub create_tempdir($cleanup = 1) {
    my $dir = tempdir('telescope-frecency-XXXX', CLEANUP => $cleanup, TMPDIR => 1);
    logger("tempdir: $dir");
    $dir;
}

my $xdg_cache_home = $ENV{XDG_CACHE_HOME} || File::Spec->catdir($ENV{HOME}, '.cache');
my $cache_dir = File::Spec->catdir($xdg_cache_home, 'telescope-frecency');
make_path($cache_dir);
my $info_json = File::Spec->catdir($cache_dir, 'info.json');
my $info = (-e $info_json && !$opt{update_libs}) ? do {
    open my $fh, '<', $info_json or die;
    local $/;
    decode_json <$fh>;
}: {};
sub prepare_lib($name) {
    my $today = localtime->ymd;
    if (defined $info->{$name} && $info->{$name}{ymd} eq $today) {
        logger("Using cached $name");
    } else {
        $info->{$name} = {
            ymd => $today,
            path => create_tempdir(0),
        };
        0 == system 'git', 'clone', "https://github.com/$name", $info->{$name}{path}
            or die "Failed to clone $name";
    }
    open my $fh, '>', $info_json or die;
    $fh->print(encode_json $info);
    $info->{$name}{path};
}

logger('Preparing libraries...');

my $telescope_path = prepare_lib('nvim-telescope/telescope.nvim');
my $plenary_path = prepare_lib('nvim-lua/plenary.nvim');

my @cmd = ($opt{executable}, '--headless', '--clean', '-u', $minimal_lua, '-c', $command);
logger("Running: @{[join ' ', @cmd]}");

my $code = system 'env',
    ($opt{verbose} ? 'DEBUG_PLENARY=1' : ()),
    'XDG_CONFIG_HOME=' . create_tempdir,
    'XDG_DATA_HOME=' . create_tempdir,
    'XDG_STATE_HOME=' . create_tempdir,
    'NVIM_LOG_FILE=' . create_tempdir . '/log',
    "TELESCOPE_PATH=$telescope_path",
    "PLENARY_PATH=$plenary_path",
    @cmd;

my $status = $code >> 8;
logger("Exit status $status");

exit $status;
